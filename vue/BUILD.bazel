load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//vite:package_json.bzl", vite_bin = "bin")
load("@npm//vue-tsc:package_json.bzl", vue_tsc_bin = "bin")

# This macro expands to a link_npm_package for each third-party package in package.json
npm_link_all_packages(name = "node_modules")

SRCS = [
    "env.d.ts",
    "index.html",
    "package.json",
    "vite.config.ts",
    "tsconfig.json",
    "tsconfig.config.json",
    "//src",
]

BUILD_DEPS = [":node_modules/" + d for d in [
    "@vitejs/plugin-vue",
    "@vitejs/plugin-vue-jsx",
    "@vue/tsconfig",
    "vite",
    "vue",
    "vue-router",
]]

vite_bin.vite(
    name = "dist",  # must be named this since it's the folder vite writes
    args = ["build"],
    output_dir = True,
    srcs = SRCS + BUILD_DEPS,
)

vue_tsc_bin.vue_tsc_test(
    name = "type-check",
    args = ["--noEmit"],
    data = SRCS + BUILD_DEPS,
)

vite_bin.vite_binary(
    name = "vite",
    data = SRCS + BUILD_DEPS,
)
